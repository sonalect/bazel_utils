"""Hermetic buf generate and staged buf_module.

Buf CLI is `@buf//:buf` from `buf.toolchains(version)` (cannot use go_binary — bufprivateusage).
Root-module `protoc.plugin` tags are put on PATH automatically. Extra
`plugins` (`buf_plugin` or other executables) are merged; the same PATH
name prefers the explicit target. `remote:` plugins and `buf.yaml` `deps`
are fetched from the BSR (needs network).
"""

load("@bazel_utils_core//internal:launcher.bzl", "SHELL_ACTION_ATTRS", "run_shell_action")
load("@protoc_root_plugins//:plugins.bzl", "ROOT_PLUGIN_LABELS")

BufGeneratedInfo = provider(
    doc = "Generated files from buf_generate.",
    fields = {
        "directory": "TreeArtifact directory of generated files.",
    },
)

BufModuleInfo = provider(
    doc = "Staged buf.yaml + protos from buf_module.",
    fields = {
        "directory": "TreeArtifact (buf.yaml + protos at workspace-relative paths).",
        "srcs": "Proto Files in the consumer workspace (format --path).",
        "config": "Consumer buf.yaml File.",
    },
)

BufDepInfo = provider(
    doc = "Import-only proto tree from buf_deps.",
    fields = {
        "staged": "dict[str, File]: import path → File after strip_import_prefix.",
    },
)

def _workspace_rel(ctx, f, what):
    """short_path of a file that must live in the consumer workspace."""
    rel = f.short_path
    if rel.startswith("../") or rel.startswith("/"):
        fail("{}: {} must be a file in the consumer workspace, got {}".format(
            ctx.label,
            what,
            rel,
        ))
    return rel

def repo_rel_from_short_path(short_path):
    """Path of a File inside its repository.

    Workspace files keep `short_path`. External files drop `../<repo>+/`.

    Args:
      short_path: Bazel `File.short_path` (`pkg/file.proto` or `../repo+/pkg/file.proto`).

    Returns:
      Path relative to that file's repository root.
    """
    if short_path.startswith("/"):
        fail("proto path must not be absolute, got {}".format(short_path))
    if short_path.startswith("../"):
        rest = short_path[len("../"):]
        slash = rest.find("/")
        if slash == -1 or slash == len(rest) - 1:
            fail("external proto path has no repo-relative file, got {}".format(short_path))
        return rest[slash + 1:]
    return short_path

def dep_import_path(repo_rel, strip_import_prefix):
    """Import path for a `buf_deps` proto after an optional repository prefix.

    `strip_import_prefix` may be `proto` or `/proto` (same meaning). Empty
    keeps the repository-relative path. The tree under that prefix is kept,
    so files in subfolders still import each other.

    Args:
      repo_rel: Repository-relative proto path (`proto/pkg/file.proto`).
      strip_import_prefix: Prefix to drop (`proto`, `/proto`, or `""`).

    Returns:
      Import path used when staging the file under the buf module root.
    """
    if not repo_rel or repo_rel.startswith("/") or "\\" in repo_rel:
        fail("dep proto path is not a relative POSIX path, got {}".format(repo_rel))
    parts = repo_rel.split("/")
    if "." in parts or ".." in parts or "" in parts:
        fail("dep proto path is not a valid import path, got {}".format(repo_rel))
    prefix = strip_import_prefix
    if prefix.startswith("/"):
        prefix = prefix[1:]
    if prefix.endswith("/"):
        prefix = prefix[:-1]
    if prefix:
        pref_parts = prefix.split("/")
        if len(parts) <= len(pref_parts) or parts[:len(pref_parts)] != pref_parts:
            fail("dep proto {} does not start with strip prefix {}".format(
                repo_rel,
                strip_import_prefix,
            ))
        parts = parts[len(pref_parts):]
    import_path = "/".join(parts)
    if not import_path.endswith(".proto"):
        fail("dep is not a .proto import path, got {}".format(import_path))
    return import_path

def _module_directory(ctx):
    """TreeArtifact from a buf_module dependency."""
    return ctx.attr.module[BufModuleInfo].directory

def _plugin_targets(ctx):
    """Root `protoc.plugin` tags plus consumer `plugins`. Same PATH name: explicit wins."""
    by_name = {}
    for target in ctx.attr._root_plugins:
        by_name[target.label.name] = target
    for target in ctx.attr.plugins:
        by_name[target.label.name] = target
    return [by_name[name] for name in sorted(by_name.keys())]

def _plugin_path_lines(ctx):
    """Write PATH wrappers that exec Bazel-built local plugins.

    Wrappers live in `$PLUGIN_BIN`, which `_workdir_lines` places next to
    (not inside) `$WORKDIR` so `find` during generate does not see them.

    Unix: extensionless bash wrapper named after the target (buf LookPath).
    Windows (busybox-w32 sh): Go LookPath only finds PATHEXT names (`.exe`,
    `.bat`, …). Copy the plugin as `name.exe` when it is an `.exe` (stdin stays
    on the binary); otherwise write a `.bat` that runs the real path.
    """
    lines = [
        "install_plugin_on_path() {",
        '  local name="$1"',
        '  local plugin="$2"',
        '  if [[ "$_WINDOWS" == 1 ]]; then',
        '    if [[ "$plugin" == *.exe ]]; then',
        '      cp -f "$plugin" "$PLUGIN_BIN/${name}.exe"',
        "    else",
        "      local win",
        "      win=$(printf '%s' \"$plugin\" | tr / '\\\\')",
        "      printf '@echo off\\r\\n\"%s\" %%*\\r\\n' \"$win\" > \"$PLUGIN_BIN/${name}.bat\"",
        "    fi",
        "    return",
        "  fi",
        # Quote $@ so it is expanded when buf invokes the wrapper, not when
        # this function writes the wrapper (unquoted EOF would bake in "").
        '  cat > "$PLUGIN_BIN/$name" <<EOF',
        "#!/usr/bin/env bash",
        'exec "$plugin" "\\$@"',
        "EOF",
        '  chmod +x "$PLUGIN_BIN/$name"',
        "}",
    ]
    for i, target in enumerate(_plugin_targets(ctx)):
        exe = target[DefaultInfo].files_to_run.executable
        if not exe:
            fail("{}: plugin {} has no executable".format(ctx.label, target.label))
        name = target.label.name
        lines.append('PLUGIN_{}="$(realpath "{}")"'.format(i, exe.path))
        lines.append('install_plugin_on_path "{}" "$PLUGIN_{}"'.format(name, i))
    lines.extend([
        'export PATH="$PLUGIN_BIN${_PATHSEP}$PATH"',
        'export HOME="$HOME_DIR"',
        'export BUF_CACHE_DIR="$BUF_CACHE_DIR"',
    ])
    return lines

def _workdir_lines(ctx, buf_bin, module_dir):
    """Copy buf_module into a workdir; cache/home/plugins sit beside it."""
    prefix = ctx.label.name
    return [
        "set -euo pipefail",
        # busybox-w32 on a Windows exec platform: `;` PATH, `.exe`/`.bat` plugins.
        'case "$(uname -s)" in Windows_NT) _WINDOWS=1; _PATHSEP=";" ;; *) _WINDOWS=0; _PATHSEP=":" ;; esac',
        'BUF="$(realpath "{}")"'.format(buf_bin.path),
        'WORKDIR="$PWD/{}.work"'.format(prefix),
        'PLUGIN_BIN="$PWD/{}.plugin_bin"'.format(prefix),
        'HOME_DIR="$PWD/{}.home"'.format(prefix),
        'BUF_CACHE_DIR="$PWD/{}.buf-cache"'.format(prefix),
        'rm -rf "$WORKDIR" "$PLUGIN_BIN" "$HOME_DIR" "$BUF_CACHE_DIR"',
        'mkdir -p "$WORKDIR" "$PLUGIN_BIN" "$HOME_DIR" "$BUF_CACHE_DIR"',
        'cp -a "{}/." "$WORKDIR/"'.format(module_dir.path),
        'chmod -R u+w "$WORKDIR"',
    ]

def _run_buf(ctx, *, module_dir, outputs, extra_inputs, extra_tools, lines, mnemonic, progress_message):
    """Run hermetic `$BUF ...` with a prebuilt buf CLI over a staged module."""
    buf_bin = ctx.executable.buf
    plugin_tools = [
        t[DefaultInfo].files_to_run
        for t in _plugin_targets(ctx)
    ]
    env = {}
    token = ctx.configuration.default_shell_env.get("BUF_TOKEN")
    if token:
        env["BUF_TOKEN"] = token
    run_shell_action(
        ctx,
        outputs = outputs,
        inputs = depset(
            direct = [module_dir] + extra_inputs,
        ),
        tools = [buf_bin] + extra_tools + plugin_tools,
        command = "\n".join(_workdir_lines(ctx, buf_bin, module_dir) + lines),
        mnemonic = mnemonic,
        progress_message = progress_message,
        env = env,
        use_default_shell_env = True,
        execution_requirements = {"requires-network": "1"},
    )

_MODULE_ATTR = attr.label(
    mandatory = True,
    providers = [BufModuleInfo],
    doc = "buf_module (staged buf.yaml + protos).",
)

_BUF_ATTR = attr.label(
    default = Label("@buf//:buf"),
    executable = True,
    cfg = "exec",
    allow_single_file = True,
    doc = "Prebuilt Buf CLI from GitHub releases (override to use another).",
)

def _copy_generated_lines():
    """Run buf generate and copy the files it wrote (common parent of new files)."""
    return [
        "find . -type f -print | sort > \"$WORKDIR.before_files\"",
        '"$BUF" generate --template buf.gen.yaml',
        "find . -type f -print | sort > \"$WORKDIR.after_files\"",
        "comm -13 \"$WORKDIR.before_files\" \"$WORKDIR.after_files\" > \"$WORKDIR.new_files\"",
        'if [[ ! -s "$WORKDIR.new_files" ]]; then',
        '  echo "buf_generate: buf generate wrote no files" >&2',
        "  exit 1",
        "fi",
        "COMMON=",
        "while IFS= read -r f; do",
        '  d=$(dirname "$f")',
        '  if [[ -z "$COMMON" ]]; then',
        '    COMMON="$d"',
        "  else",
        '    while [[ "$d" != "$COMMON" && "$d" != "$COMMON"/* ]]; do',
        '      if [[ "$COMMON" == "." ]]; then',
        '        echo "buf_generate: generated files do not share a directory" >&2',
        "        exit 1",
        "      fi",
        '      COMMON=$(dirname "$COMMON")',
        "    done",
        "  fi",
        'done < "$WORKDIR.new_files"',
        'if [[ -z "$COMMON" || "$COMMON" == "." ]]; then',
        '  echo "buf_generate: generated files do not share a directory" >&2',
        "  exit 1",
        "fi",
        'cp -a "$COMMON/." "$OUT/"',
    ]

def _buf_generate_impl(ctx):
    out_dir = ctx.actions.declare_directory(ctx.label.name)
    module_dir = _module_directory(ctx)

    lines = _plugin_path_lines(ctx) + [
        'cp "{}" "$WORKDIR/buf.gen.yaml"'.format(ctx.file.template.path),
        'mkdir -p "{}"'.format(out_dir.path),
        'OUT="$(realpath "{}")"'.format(out_dir.path),
        'cd "$WORKDIR"',
        # `buf dep update` warns "No configured dependencies were found to
        # update" when buf.yaml has no `deps:` (e.g. all deps moved to
        # Bazel deps). Only run it when there is something to resolve.
        'if grep -q "^deps:" buf.yaml; then "$BUF" dep update; fi',
    ] + _copy_generated_lines()

    _run_buf(
        ctx,
        module_dir = module_dir,
        outputs = [out_dir],
        extra_inputs = [ctx.file.template],
        extra_tools = [],
        lines = lines,
        mnemonic = "BufGenerate",
        progress_message = "Generating %{label} with buf",
    )
    return [
        DefaultInfo(files = depset([out_dir])),
        BufGeneratedInfo(directory = out_dir),
    ]

buf_generate = rule(
    implementation = _buf_generate_impl,
    doc = """`buf generate` over a buf_module.

Root-module `protoc.plugin` tags are put on PATH automatically. Extra
`plugins` (`buf_plugin` or other executables) are merged; the same PATH
name prefers the explicit target. Target name is the PATH name (`local:`
in the template). `remote:` plugins in the template are fetched from the BSR
(the action requires network). `buf dep update` resolves `buf.yaml`
`deps` into the action workdir.

The template is passed to `buf generate --template` as-is (`out`,
`include_imports`, `include_wkt`, `inputs`). This rule does not parse it.
Generated files must share a single directory (one TreeArtifact); use a
separate `buf_generate` per template when `out` paths are unrelated.

Cache, HOME, and plugin PATH wrappers sit beside the workdir so they are
not copied into the output.

BSR fetches (`buf dep update`, `remote:` plugins) need `BUF_TOKEN` in the
action env: `build --action_env=BUF_TOKEN` (sandbox does not inherit the
user shell).

Returns a directory TreeArtifact of the files buf wrote and BufGeneratedInfo
for write_source_files.
""",
    attrs = {
        "module": _MODULE_ATTR,
        "template": attr.label(
            allow_single_file = True,
            mandatory = True,
            doc = "buf.gen.yaml passed to `buf generate --template`.",
        ),
        "plugins": attr.label_list(
            cfg = "exec",
            allow_files = True,
            doc = "Extra local plugins put on PATH (`buf_plugin` or any executable). Merged with the root module's `protoc.plugin` tags. Same PATH name: this list wins. Target name is the PATH name (`local:` in the template).",
        ),
        "_root_plugins": attr.label_list(
            default = ROOT_PLUGIN_LABELS,
            cfg = "exec",
            allow_files = True,
            doc = "Root-module `protoc.plugin` tags (`@protoc_root_plugins//:<name>`).",
        ),
        "buf": _BUF_ATTR,
    } | SHELL_ACTION_ATTRS,
)

def _buf_deps_impl(ctx):
    """Map srcs to import paths with this target's strip_import_prefix."""
    staged = {}
    strip = ctx.attr.strip_import_prefix
    for src in ctx.files.srcs:
        rel = dep_import_path(repo_rel_from_short_path(src.short_path), strip)
        if rel in staged:
            fail("{}: duplicate import path {}".format(ctx.label, rel))
        staged[rel] = src
    return [
        DefaultInfo(files = depset(ctx.files.srcs)),
        BufDepInfo(staged = staged),
    ]

_buf_deps = rule(
    implementation = _buf_deps_impl,
    doc = """Import-only proto tree. One include root (`strip_import_prefix`).

Subfolders stay under that root, so files can import each other. Pass the
target to `buf_module` `deps`. Not formatted.
""",
    attrs = {
        "srcs": attr.label_list(
            allow_files = [".proto"],
            mandatory = True,
            doc = "Protobuf sources in this or another repository (file, filegroup, or glob).",
        ),
        "strip_import_prefix": attr.string(
            default = "",
            doc = "Repository-relative include root to drop (`proto` and `/proto` are the same). Empty keeps the path inside that repository.",
        ),
    },
)

def buf_deps(name, srcs, strip_import_prefix = "", **kwargs):
    """Import-only proto tree for `buf_module` `deps`."""
    _buf_deps(
        name = name,
        srcs = srcs,
        strip_import_prefix = strip_import_prefix,
        **kwargs
    )

def _buf_module_impl(ctx):
    """Stage workspace srcs and import-only deps under the consumer buf.yaml."""
    _workspace_rel(ctx, ctx.file.config, "buf.yaml")
    staged = {}
    dep_files = []
    for src in ctx.files.srcs:
        rel = _workspace_rel(ctx, src, "proto")
        if rel in staged:
            fail("{}: duplicate proto path {}".format(ctx.label, rel))
        staged[rel] = src
    for dep in ctx.attr.deps:
        for rel, src in dep[BufDepInfo].staged.items():
            if rel in staged:
                fail("{}: dep import path {} collides with another proto".format(
                    ctx.label,
                    rel,
                ))
            staged[rel] = src
            dep_files.append(src)
    out = ctx.actions.declare_directory(ctx.label.name)
    lines = [
        "set -euo pipefail",
        'OUT="{}"'.format(out.path),
        'rm -rf "$OUT"',
        'mkdir -p "$OUT"',
        'cp "{}" "$OUT/buf.yaml"'.format(ctx.file.config.path),
    ]
    for rel in sorted(staged.keys()):
        src = staged[rel]
        lines.append('mkdir -p "$OUT/$(dirname "{}")"'.format(rel))
        lines.append('cp "{}" "$OUT/{}"'.format(src.path, rel))
    run_shell_action(
        ctx,
        outputs = [out],
        inputs = depset(direct = [ctx.file.config] + ctx.files.srcs + dep_files),
        command = "\n".join(lines),
        mnemonic = "BufModule",
        progress_message = "Staging buf module %{label}",
        use_default_shell_env = True,
    )
    return [
        DefaultInfo(files = depset([out])),
        BufModuleInfo(
            directory = out,
            srcs = ctx.files.srcs,
            config = ctx.file.config,
        ),
    ]

_buf_module = rule(
    implementation = _buf_module_impl,
    doc = """Directory with staged buf.yaml, workspace srcs, and import-only deps.

`config` is copied as-is (BSR `deps` stay remote). Fetch happens in generate/lint.
`deps` are not formatted: `buf_format` still walks workspace `srcs` only.
""",
    attrs = {
        "srcs": attr.label_list(
            allow_files = [".proto"],
            mandatory = True,
            doc = "Protobuf sources (paths preserved under the module root).",
        ),
        "deps": attr.label_list(
            providers = [BufDepInfo],
            default = [],
            doc = "`buf_deps` trees staged at their import paths. Not passed to `buf_format`. Distinct from `buf.yaml` `deps` (BSR modules).",
        ),
        "config": attr.label(
            allow_single_file = True,
            mandatory = True,
            doc = "Consumer buf.yaml.",
        ),
    } | SHELL_ACTION_ATTRS,
)

def buf_module(
        name,
        srcs,
        config = "//:buf.yaml",
        deps = [],
        **kwargs):
    """Stage protos plus the consumer buf.yaml (default `//:buf.yaml`)."""
    _buf_module(
        name = name,
        srcs = srcs,
        config = config,
        deps = deps,
        **kwargs
    )

# Re-export for lint.bzl (same TreeArtifact helper).
module_directory = _module_directory
