"""Workspace golangci-lint: cd to the consumer repo and run this module's binary."""

load("@bazel_utils_core//internal:workspace_tool.bzl", "manifest_label", "workspace_file_label", "workspace_test_tags")
load("//:dirs.bzl", "GO_MODULE_DIR", "go_list_patterns")
load("//:go_workspace_tool.bzl", "go_workspace_tool_rule")

_golangci_test = go_workspace_tool_rule(
    tool_attr = "golangci",
    tool_default = Label("//:golangci-lint"),
    tool_doc = "Prebuilt golangci-lint from GitHub releases (override to use another).",
    flags_doc = "golangci-lint arguments after `run` and before package dirs.",
    doc = "bazel test: golangci-lint against the workspace (no-sandbox).",
    use_manifest = True,
    pre_exec = GO_MODULE_DIR,
    use_config = True,
    config_flag = "--config",
)

def golangci_test(
        name,
        workspace = "//:MODULE.bazel",
        manifest = "//:go.mod",
        config = "//:.golangci.yaml",
        tags = [],
        dirs = [],
        flags = [],
        local = True,
        **kwargs):
    """Test that runs bazel_utils's golangci-lint after cd to the consumer workspace.

    The linter binary is the GitHub release for the exec OS/CPU. `go list` still uses the
    consumer's rules_go SDK so analysis matches the code under test.

    Invokes `golangci-lint --config <config> run <flags> <dirs>` from the workspace root.
    Defaults `local = True` (sandbox has GOPROXY=off) and tags to `external`,
    `no-cache`, `no-sandbox`, `requires-network`.

    Args:
      name: Target name.
      workspace: Repo-root marker file (used when BUILD_WORKSPACE_DIRECTORY is unset).
      manifest: Consumer go.mod from repo root (default `//:go.mod`; also `//go/go.mod`).
      config: Consumer golangci config (default `//:.golangci.yaml`).
      tags: Extra test tags; merged with the defaults above.
      dirs: Bazel paths from repo root (e.g. `["//go/app"]`). The tool runs in the
        `manifest` directory, so they become patterns relative to it
        (`./app/...` for `//go:go.mod`). Empty checks the whole module.
      flags: Extra golangci-lint flags after `run` and before `dirs`.
      local: Run outside the sandbox (default True).
      **kwargs: Forwarded to the test rule (`golangci`, `size`, …).
    """
    _golangci_test(
        name = name,
        workspace = workspace,
        manifest = manifest_label(manifest),
        config = workspace_file_label(config, what = "config"),
        tags = workspace_test_tags(tags, requires_network = True),
        flags = ["run"] + flags + go_list_patterns(dirs, manifest_label(manifest)),
        local = local,
        **kwargs
    )
